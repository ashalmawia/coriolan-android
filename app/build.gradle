apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: "kotlin-allopen"
apply plugin: 'io.fabric'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
def keystoreExists = keystorePropertiesFile.exists()
if (keystoreExists) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    compileSdkVersion COMMON__COMPILE_SDK
    buildToolsVersion COMMON__BUILD_TOOLS
    defaultConfig {
        applicationId "com.ashalmawia.coriolan"
        minSdkVersion 17
        targetSdkVersion COMMON__TARGET_SDK
        versionCode 8
        versionName "0.8-alpha"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            storeFile (keystoreExists ? file(keystoreProperties['storeFile']) : null)
            storePassword (keystoreExists ? keystoreProperties['storePassword'] : "")
            keyAlias (keystoreExists ? keystoreProperties['keyAlias'] : "")
            keyPassword (keystoreExists ? keystoreProperties['keyPassword'] : "")
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    lintOptions {
        disable 'ContentDescription', 'RtlSymmetry'
        disable 'AppCompatResource', 'AlwaysShowAction'
    }
    testOptions {
        unitTests.includeAndroidResources = true
    }
}

allOpen {
    annotation("com.ashalmawia.coriolan.util.OpenForTesting")
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:recyclerview-v7:$support_version"
    implementation "com.android.support:design:$support_version"
    implementation "com.android.support:cardview-v7:$support_version"
    implementation "com.android.support:gridlayout-v7:$support_version"
    implementation "com.takisoft.fix:preference-v7:$support_version.0"
    implementation('com.crashlytics.sdk.android:crashlytics:2.8.0@aar') {
        transitive = true
    }
    implementation project(':errors')
    implementation group: 'joda-time', name: 'joda-time', version: '2.9.7'
    implementation("com.aurelhubert:ahbottomnavigation:2.1.0") {
        exclude group: "com.android.support", module: "design"
    }
    implementation 'com.fasterxml.jackson.core:jackson-core:2.9.5'

    implementation "com.squareup.moshi:moshi:$moshi_version"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshi_version"

    // Koin for Kotlin
    implementation "org.koin:koin-core:$koin_version"
    // Koin for Unit tests
    testImplementation "org.koin:koin-test:$koin_version"
    // Koin for Android
    implementation "org.koin:koin-android:$koin_version"

    testImplementation 'junit:junit:4.12'
    testImplementation "org.robolectric:robolectric:3.6.1"
    testImplementation 'org.mockito:mockito-core:2.8.47'
    testImplementation "com.nhaarman:mockito-kotlin-kt1.1:1.5.0"

    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation "com.android.support:support-annotations:$support_version"

    implementation ("com.github.hotchemi:permissionsdispatcher:3.2.0") {
        exclude module: "support-v13"
    }
    kapt "com.github.hotchemi:permissionsdispatcher-processor:3.2.0"
}
